plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'dagger.hilt.android.plugin'
  id("androidx.navigation.safeargs.kotlin")
}

def homePath = System.properties['user.home']
def secretsPropertiesFile = file("$homePath/.gradle/gradle.properties")
def secretProperties = new Properties()
secretProperties.load(new FileInputStream(secretsPropertiesFile))

android {
  compileSdk 31

  defaultConfig {
    applicationId "com.ruizurraca.marveltest"
    minSdk 22
    targetSdk 31
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    buildConfigField "String", "BASE_URL", "\"https://gateway.marvel.com/\""

    buildConfigField "String", "MARVEL_PUBLIC_KEY", "\"${secretProperties['MARVEL_PUBLIC_KEY']}\""
    buildConfigField "String", "MARVEL_PRIVATE_KEY", "\"${secretProperties['MARVEL_PRIVATE_KEY']}\""
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }
  viewBinding {
    enabled = true
  }
  buildFeatures {
    dataBinding true
  }
  testOptions {
    unitTests.all {
      useJUnitPlatform()
    }
    unitTests.returnDefaultValues = true
  }
  dynamicFeatures = [':list']
}

dependencies {

  implementation "androidx.core:core-ktx:$core_ktx_version"
  implementation "androidx.appcompat:appcompat:$appcompat_version"
  implementation "com.google.android.material:material:$material_version"
  implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

  // Testing
  testImplementation "io.mockk:mockk:$mockk_version"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiter_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiter_version"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiter_version"
  testImplementation "com.willowtreeapps.assertk:assertk-jvm:$assertk_jvm_version"

  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // DAGGER
  implementation "com.google.dagger:hilt-android:$dagger_version"
  kapt "com.google.dagger:hilt-android-compiler:$dagger_version"

  //Navigation
  implementation "androidx.navigation:navigation-fragment-ktx:$nav_version_ktx"
  implementation "androidx.navigation:navigation-ui-ktx:$nav_version_ktx"
  implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version_ktx"


  // HTTP
  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
  implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"
  implementation "com.google.code.gson:gson:$gson_version"
  implementation "com.squareup.retrofit2:converter-gson:$converter_gson_version"

  // IMAGES
  implementation "com.github.bumptech.glide:glide:$glide_version"
  kapt "com.github.bumptech.glide:compiler:$glide_version"
}